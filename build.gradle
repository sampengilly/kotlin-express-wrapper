group 'com.github.sampengilly'
version '0.1.0-SNAPSHOT'

buildscript {
    apply from: 'versions.gradle'

    repositories {
        maven { url "https://dl.bintray.com/kotlin/kotlin-dev" }
        maven { url "https://kotlin.bintray.com/kotlinx" }
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.moowork.gradle:gradle-node-plugin:$node_plugin_version"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$bintray_plugin_version"
        classpath "org.jetbrains.kotlinx:kotlinx-gradle-serialization-plugin:$kotlinx_serialization_version"
    }
}

subprojects {
    repositories {
        maven { url "https://dl.bintray.com/kotlin/kotlin-dev" }
        maven { url "https://kotlin.bintray.com/kotlinx" }
        jcenter()
        maven {
            url "http://dl.bintray.com/kotlin/kotlin-js-wrappers"
        }
    }

    def projectName = name
    def outputJsFile = "$projectDir/build/classes/main/${projectName}.js"
    ext.outputFile = outputJsFile
    
    ext.applyKotlinJs = {
        apply plugin: "kotlin2js"
        
        dependencies {
            compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
        }

        compileKotlin2Js {
            kotlinOptions {
                outputFile = outputJsFile
                moduleKind = "commonjs"
                sourceMap = true
                sourceMapEmbedSources = "always"
            }
        }
    }
    
    ext.configurePublishing = { 
        apply plugin: 'com.moowork.node'
        apply plugin: 'com.jfrog.bintray'
        apply plugin: 'maven-publish'
        apply plugin: 'java'
        
        task sourcesJar(type: Jar) {
            classifier = "sources"
            from sourceSets.main.allSource
        }
        
        artifacts {
            archives sourcesJar
        }
        
        def v = "0.1.0-kotlin-$kotlin_version"
        bintray {
            user = System.getenv("BINTRAY_USER")
            key = System.getenv("BINTRAY_KEY")
            publish = true
            pkg {
                repo = "kotlin-express-wrapper"
                name = "kotlin-express-wrapper"
                userOrg = "com.github.sampengilly"
                licenses = ["Apache-2.0"]
                vcsUrl = "https://github.com/sampengilly/kotlin-express-wrapper.git"
                version {
                    name = v
                }
            }
            publications = ["Publication"]
        }
        
        publishing {
            publications {
                Publication(MavenPublication) {
                    from components.java
                    groupId "com.github.sampengilly"
                    artifactId "kotlin-express-wrapper"
                    version v
                    
                    artifact sourcesJar
                }
            }
        }
    }
}
